

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТабличныйДокумент.ФиксацияСверху = 1;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	СоставВерсии = СтрРазделить(СистемнаяИнформация.ВерсияПриложения, ".");
	НомерРелизаПлатформы = СоставВерсии[2];
	
	МассивМинимальноВерсии = Новый Массив;
	МассивМинимальноВерсии.Добавить(3);
	МассивМинимальноВерсии.Добавить(1);
	МассивМинимальноВерсии.Добавить(3);
	
	ТекстОшибки = "";
	Если Метаданные.ОбщиеМодули.Найти("СтандартныеПодсистемыСервер") = Неопределено Тогда
		ВызватьИсключение "Отсутсвует БСП";
	Иначе
		Результат = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
		МассивВерсии = СтрРазделить(Результат, ".");
		Если Не (Число(МассивВерсии[0]) >= МассивМинимальноВерсии[0] 
			И Число(МассивВерсии[1]) >= МассивМинимальноВерсии[1] 
			И Число(МассивВерсии[2]) >= МассивМинимальноВерсии[2]) Тогда
			
			ВызватьИсключение "Текущая версия БСП не поддерживается";	
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаплолнитьНастройкиПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрНайти(ПараметрЗапуска, "Загрузить", , 1) Тогда
		
		Отказ = Истина;
		
		ПутьКФайлу = СокрЛП(СтрЗаменить(ПараметрЗапуска, "Загрузить", ""));
		
		ДанныеИмпорта = ПрочитатьДанныеИзJSON(ПутьКФайлу);
		ЗагрузитьПользователейНаСервере(ДанныеИмпорта.ДанныеПользователей);

		ЗавершитьРаботуСистемы(Ложь);
		Возврат;
		
	ИначеЕсли СтрНайти(ПараметрЗапуска, "Генерировать", , 1) Тогда
		
		Отказ = Истина;
		// TODO: Генерация по настройкам из файла.
		// REF: Результатом генерации должен быть массив из ДанныеПользователей. На вход подаются структура параметров
		//			Заполнение Табличного токумента переделать на заполнение из массив из ДанныеПользователей.
		ЗавершитьРаботуСистемы(Ложь);
		Возврат;
		
	КонецЕсли;
	
	УстановитьОтображениеЭлементов();
	
КонецПроцедуры  

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТабличныйДокументПриИзменении(Элемент)
	
	Если Не Автозаполнение Тогда
		Возврат;
	КонецЕсли;
	
	НомерПервойСтроки		= Элементы.ТабличныйДокумент.ТекущаяОбласть.Верх;
	НомерПоследнейСтроки	= Элементы.ТабличныйДокумент.ТекущаяОбласть.Низ;
	
	Если НомерПервойСтроки = 0 Или НомерПоследнейСтроки = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НомерКолонки = Элементы.ТабличныйДокумент.ТекущаяОбласть.Лево;
	
	МассивУникальныхИмен = Новый Массив;
	
	Если НомерКолонки = 1 Тогда
		Для НомерСтроки = НомерПервойСтроки По НомерПоследнейСтроки Цикл
			ЗаполнитьПараметрыВСтроке(НомерСтроки, МассивУникальныхИмен);
		КонецЦикла;
	КонецЕсли;
	
	ПроверитьУникальностьЗначений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольХранимоеЗначениеПриИзменении(Элемент)
	
	УстановитьОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантУстановкиДоступаПриИзменении(Элемент)
	
	Если ВариантУстановкиДоступа = 1 Тогда
		ДанныеПравДоступа = "";
		ТабличныйДокумент.Область("ПраваДоступа").Текст = "Группы доступа";
	Иначе
		ДанныеПравДоступа = ПолучитьИмяТекущегоПользователя();
		ТабличныйДокумент.Область("ПраваДоступа").Текст = "Пользователь основание";
	КонецЕсли;
	
	ЗаполнитьДанныеКолонки(Колонки.ПраваДоступа, ДанныеПравДоступа, Ложь); //Истина
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПравДоступаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВариантУстановкиДоступа = 1 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора",		Истина);
		ПараметрыФормы.Вставить("МножественныйВыбор",	Истина);
		
		ПараметрыОтбора = Новый Структура("Пользователь", Неопределено);
		ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОповещенияВыбораПравДоступа", ЭтотОбъект);
		ОткрытьФорму("Справочник.ГруппыДоступа.Форма.ФормаСписка", ПараметрыФормы, Элемент, ЭтаФорма, , , ОповещениеОЗакрытии);
	ИначеЕсли ВариантУстановкиДоступа = 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("МножественныйВыбор",	Ложь);
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОповещенияВыбораПравДоступа", ЭтотОбъект);
		ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элемент, ЭтаФорма, , , ОповещениеОЗакрытии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнениеПриИзменении(Элемент)
	
	УстановитьОтображениеЭлементов();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ИмпортироватьИзФайла(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр = "Файл JSON(*.json)|*.json";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	
	РезультатВыбора = Ждать ДиалогВыбораФайла.ВыбратьАсинх();
	
	Если РезультатВыбора.Количество() = 1 Тогда
	
		ДанныеИмпорта = ПрочитатьДанныеИзJSON(РезультатВыбора[0]);
		
		УстановитьНастройки(ДанныеИмпорта.Настройки);
		УстановитьДанныеТабличногоДокумента(ДанныеИмпорта.ДанныеПользователей);
		
		ПоказатьПредупреждение(, "Данные загружены");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЭкспортироватьВФайл(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.ПолноеИмяФайла = ""; // Указать имя базы
	ДиалогВыбораФайла.Фильтр = "Файл JSON(*.json)|*.json";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	
	РезультатВыбора	= Ждать ДиалогВыбораФайла.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьДанныеВJSON(РезультатВыбора[0]);
	
	ПоказатьПредупреждение(, "Данные сохранены");
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ОчиститьПолеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСлучайныхПользователей(Команда)
	
	НомерПервойСтроки		= ТабличныйДокумент.ВысотаТаблицы + 1;
	//TODO Пределать на процедуру, для улучшения читаемости.
	НастройкиЗаполнения		= ПолучитьНастройки();
	ТабличныйДокумент		= ЗаполнитьСлучайныхПользователейНаСервере(НастройкиЗаполнения);
	НомерПоследнейСтроки	= ТабличныйДокумент.ВысотаТаблицы;
	
	МассивУникальныхИмен = Новый Массив;
	
	Для НомерСтроки = НомерПервойСтроки По НомерПоследнейСтроки Цикл
		ЗаполнитьПараметрыВСтроке(НомерСтроки, МассивУникальныхИмен);
	КонецЦикла;

	//ПроверитьУникальностьЗначений();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователя(Команда)
	
	ВыполнитьСозданиеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПользователей(Команда)
	
	ДанныеДляЗагрузки = ПолучитьДанныеТабличногоДокумента();
	ЗагрузитьПользователейНаСервере(ДанныеДляЗагрузки);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытьНастройки(Команда)
	
	Элементы.ГруппаНастройки.Видимость = Не Элементы.ГруппаНастройки.Видимость;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ЗаплолнитьНастройкиПоУмолчанию()

	Автозаполнение = Истина;
	
	АутентификацияСтандартная		= Истина;
	ПотребоватьСменуПароляПриВходе	= Ложь;
	ПоказыватьВСпискеВыбора		= Истина;
	ЗапрещеноИзменятьПароль		= Ложь;
	
	АутентификацияОС				= Истина;
	АутентификацияOpenID			= Ложь;
	АутентификацияOpenIDConnect	= Ложь;
	
	ШаблонПочты	= "%i%o%F@mail.me";
	ШаблонДомена	= "\\localhost\%i%o%F";
	
	
	ПарольХранимоеЗначение	= 0;
	ВариантУстановкиДоступа	= 0;
	
	ДанныеПравДоступа = ПолучитьИмяТекущегоПользователя();
		
	КоличествоПользователей = 100; 
	
	Колонки = Новый Структура;
	Колонки.Вставить("ПолноеИмя",						1);
	Колонки.Вставить("Имя",							2);
	Колонки.Вставить("АдресЭлектроннойПочты",			3);
	Колонки.Вставить("ПользовательОС",					4);
	Колонки.Вставить("Пароль",							5);
	Колонки.Вставить("ПраваДоступа",					6);
	Колонки.Вставить("Комментарий",						7);
	Колонки.Вставить("ПоказыватьВСпискеВыбора",			8);
	Колонки.Вставить("ПотребоватьСменуПароляПриВходе",	9);
	Колонки.Вставить("ЗапрещеноИзменятьПароль",			10);
	Колонки.Вставить("ЗапрещеноВосстанавливатьПароль",	11);
	Колонки.Вставить("АутентификацияСтандартная",		12);
	Колонки.Вставить("АутентификацияОС",				13);
	Колонки.Вставить("АутентификацияOpenID",			14);
	Колонки.Вставить("АутентификацияOpenIDConnect",		15);
	Колонки.Вставить("АутентификацияТокеномДоступа",		16);
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяТекущегоПользователя()

	#Если Клиент Тогда
	
	ТекущийПользователь	= ПользователиКлиент.ТекущийПользователь();
	ИмяПользователя		= Строка(ТекущийПользователь);
	
	#ИначеЕсли Сервер Тогда

	ТекущийПользователь	= Пользователи.ТекущийПользователь();
	ИмяПользователя		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Наименование");

	#КонецЕсли 

	Возврат ИмяПользователя;
	
КонецФункции // ПолучитьИмяТекущегоПользователя()

&НаКлиенте
Процедура УстановитьОтображениеЭлементов()
	
	Если ПарольХранимоеЗначение = 1 Тогда
		ТабличныйДокумент.Область("Пароль").Текст = "Хранимое значение";
	Иначе
		ТабличныйДокумент.Область("Пароль").Текст = "Пароль";
	КонецЕсли;
	
	Элементы.ГруппаЗаполнениеПароля.Доступность = ПарольХранимоеЗначение = 0;
	
	Элементы.ГруппаАутентификация.Доступность	= Автозаполнение;
	Элементы.ГруппаЗаполнение.Доступность		= Автозаполнение;
	Элементы.ГруппаГенерация.Доступность		= Автозаполнение;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыСтроки(ТаблицаДанных, МассивЗначений, НомерСтроки)
	
	Для ИндексПараметра = 0 По МассивЗначений.ВГраница() Цикл
		Область = ТаблицаДанных.Область(НомерСтроки, ИндексПараметра + 1, НомерСтроки, ИндексПараметра + 1);
		Область.Текст = МассивЗначений[ИндексПараметра];
		
		Если НомерСтроки = 1 Тогда
			Область.ЦветФона		= WebЦвета.СветлоСерый;
			Область.ВысотаСтроки	= 20;
			Область.ШиринаКолонки	= 25; //СтрДлина(Область.Текст) + 2;
			
			Область.ВертикальноеПоложение	= ВертикальноеПоложение.Центр;
			Область.ГоризонтальноеПоложение	= ГоризонтальноеПоложение.Центр;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСлучайныхПользователейНаСервере(НастройкиЗаполнения)
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	
	НаборНовыхПользователей = ОбъектОбработка.ПолучитьНаборСлучайныхПользователей(НастройкиЗаполнения);
	
	Если ДоступПоСлучайномуПользователю Тогда
		УстановитьСлучайныхПользователейОснованийНаСервере(НаборНовыхПользователей);
	КонецЕсли;
	
	ДополнениеТабличногоДокумента = Новый ТабличныйДокумент;
	
	СвойстваПароля = Пользователи.СвойстваПароля();
	СчетчикСтрок = 1;
	Для Каждого ФИОПользователя Из НаборНовыхПользователей Цикл
		ДополнениеТабличногоДокумента.Область(СчетчикСтрок, Колонки.ПолноеИмя, 
											СчетчикСтрок, Колонки.ПолноеИмя).Текст = НаборНовыхПользователей.ПолноеИмя;
		Если СпособУстановкиПароля = 0 Тогда
			НовыйПароль = ПолучитьНовыйПароль(СвойстваПароля, НомерРелизаПлатформы); 
		Иначе
			НовыйПароль = ПарольПоУмолчанию; 
		КонецЕсли;
		
		ДополнениеТабличногоДокумента.Область(СчетчикСтрок, Колонки.Пароль, 
											СчетчикСтрок, Колонки.Пароль).Текст = НаборНовыхПользователей.Пароль;
		
		
		ДополнениеТабличногоДокумента.Область(СчетчикСтрок, Колонки.ПраваДоступа, 
											СчетчикСтрок, Колонки.ПраваДоступа).Текст = НаборНовыхПользователей.ДанныеПравДоступа;
											
		ДополнениеТабличногоДокумента.Область(СчетчикСтрок, Колонки.Комментарий, 
											СчетчикСтрок, Колонки.Комментарий).Текст = НаборНовыхПользователей.Комментарий;

		СчетчикСтрок = СчетчикСтрок + 1;
	КонецЦикла;

//	Если ДоступПоСлучайномуПользователю Тогда
//		УстановитьСлучайныхПользователейОснованийНаСервере(ДополнениеТабличногоДокумента, Колонки.ПраваДоступа);
//	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ДополнениеТабличногоДокумента);
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ЗаполнитьСлучайныхПользователейНаСервере()

&НаСервереБезКонтекста
Процедура УстановитьСлучайныхПользователейОснованийНаСервере(НаборНовыхПользователей)

	МассивПользователей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Пользователи.Наименование КАК Пользователь,
	               |	Пользователи.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Пользователи КАК Пользователи
	               |ГДЕ
	               |	НЕ Пользователи.ПометкаУдаления
	               |	И НЕ Пользователи.Недействителен
	               |	И Пользователи.Наименование <> """"
	               |	И НЕ Пользователи.Предопределенный
	               |	И Пользователи.Наименование <> ""<Не указан>""
	               |	И НЕ Пользователи.Служебный";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Результат.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю("Пользователи не найдены");
		ВызватьИсключение "Пользователи не найдены";
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не Пользователи.ЭтоПолноправныйПользователь(Выборка.Ссылка, Истина)
			И Пользователи.ВходВПрограммуРазрешен(Выборка.Ссылка) Тогда
			МассивПользователей.Добавить(Выборка.Пользователь);
		КонецЕсли;
	
	КонецЦикла;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	//Количество = ТабличныйДокумент.ВысотаТаблицы;
	
	Для Каждого ЭлементНабора Из НаборНовыхПользователей Цикл
		СлучайноеЧисло = ГСЧ.СлучайноеЧисло(0, МассивПользователей.ВГраница());
		СлучайныйПользователь = МассивПользователей[СлучайноеЧисло];
		
//		ТабличныйДокумент.Область(СчетчикСтрок, КолонкаДанных, 
//										СчетчикСтрок, КолонкаДанных).Текст = СлучайныйПользователь;
		ЭлементНабора.ПользовательОснование = СлучайныйПользователь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьСозданиеНаСервере()

	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	
	СтруктураПараметров = ОбъектОбработка.ПолучитьСтруктуруПараметровПользователя();
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, Объект);
	ОбъектОбработка.СоздатьПолизователяПоПараметрам(СтруктураПараметров);

КонецПроцедуры // ВыполнитьСозданиеНаСервере()

&НаКлиенте
Функция ПолучитьДанныеТабличногоДокумента()

	СоставПользователей = Новый Массив;
	СписокПолейЗаполнеия = ПоляЗаполнения();
	
	КоличествоСтрок = ТабличныйДокумент.ВысотаТаблицы;
	Для СчетчикСтрок = 2 По КоличествоСтрок Цикл
		
		ОписаниеПользователя = ПолучитьОписаниеПользователя();
		
		ЗаполнитьЗначенияПоДаннымСтроки(ОписаниеПользователя, СчетчикСтрок, СписокПолейЗаполнеия);
		
		Пароль		= ТабличныйДокумент.Область(СчетчикСтрок, Колонки.Пароль, СчетчикСтрок, Колонки.Пароль).Текст;
		ПраваДоступа	= ТабличныйДокумент.Область(СчетчикСтрок, Колонки.ПраваДоступа, СчетчикСтрок, Колонки.ПраваДоступа).Текст;
		
		Если ПарольХранимоеЗначение = 0 Тогда
			ОписаниеПользователя.Пароль = Пароль;
			ОписаниеПользователя.ПарольУстановлен = Ложь;
			ОписаниеПользователя.СохраняемоеЗначениеПароля = "";
		Иначе
			ОписаниеПользователя.Пароль = "";
			ОписаниеПользователя.ПарольУстановлен = Истина;
			ОписаниеПользователя.СохраняемоеЗначениеПароля = Пароль;
		КонецЕсли;
		
		ОписаниеПользователя.ГруппыДоступа = "";
		ОписаниеПользователя.ПользовательОснование = "";
		
		Если ВариантУстановкиДоступа = 0 Тогда
			ОписаниеПользователя.ПользовательОснование = ПраваДоступа;
		ИначеЕсли ВариантУстановкиДоступа = 1 Тогда
			ОписаниеПользователя.ГруппыДоступа = ПраваДоступа;
		КонецЕсли;
		
		СоставПользователей.Добавить(ОписаниеПользователя);
		
	КонецЦикла;
	
	Возврат СоставПользователей;

КонецФункции

&НаКлиенте
Процедура ЗаполнитьЗначенияПоДаннымСтроки(ОписаниеПользователя, НомерСтроки, ПоляЗаполнения)

	Для Каждого ИмяПоля Из ПоляЗаполнения Цикл
		
		ОбластьЗначения = ТабличныйДокумент.Область(НомерСтроки, Колонки[ИмяПоля], 
												НомерСтроки, Колонки[ИмяПоля]);
		Если ТипЗнч(ОписаниеПользователя[ИмяПоля]) = Тип("Булево") Тогда
			ОписаниеПользователя[ИмяПоля] = Не ПустаяСтрока(ОбластьЗначения.Текст);
		Иначе
			ОписаниеПользователя[ИмяПоля] = ОбластьЗначения.Текст;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьЗначенияПоДаннымСтроки()

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьОписаниеПользователя()
	
	ОписаниеПользователя = Новый Структура;
	ОписаниеПользователя.Вставить("Имя", "");
	ОписаниеПользователя.Вставить("ПолноеИмя", "");
	ОписаниеПользователя.Вставить("АдресЭлектроннойПочты", "");
	
	ОписаниеПользователя.Вставить("Пароль", "");
	ОписаниеПользователя.Вставить("СохраняемоеЗначениеПароля", "");
	
	ОписаниеПользователя.Вставить("ПоказыватьВСпискеВыбора", Ложь);
	ОписаниеПользователя.Вставить("ЗапрещеноИзменятьПароль", Ложь);
	ОписаниеПользователя.Вставить("ЗапрещеноВосстанавливатьПароль", Ложь);
	
	//Если свойство ПарольУстановлен = Ложь, тогда в незаполненное свойство СохраняемоеЗначениеПароля устанавливается новое сгенерированное значение пароля
	ОписаниеПользователя.Вставить("ПарольУстановлен", Ложь); 
	
	ОписаниеПользователя.Вставить("ПользовательОС", "");
	
	ОписаниеПользователя.Вставить("ПотребоватьСменуПароляПриВходе", Ложь);
	ОписаниеПользователя.Вставить("АутентификацияСтандартная", Ложь);
	ОписаниеПользователя.Вставить("АутентификацияOpenID", Ложь);
	ОписаниеПользователя.Вставить("АутентификацияOpenIDConnect", Ложь);
	ОписаниеПользователя.Вставить("АутентификацияТокеномДоступа", Ложь);
	ОписаниеПользователя.Вставить("АутентификацияОС", Ложь);
	
	ОписаниеПользователя.Вставить("ГруппыДоступа", "");
	ОписаниеПользователя.Вставить("ПользовательОснование", "");
	ОписаниеПользователя.Вставить("Комментарий", "");
	
	ОписаниеПользователя.Вставить("РежимЗапуска", "Авто");
	ОписаниеПользователя.Вставить("ЗащитаОтОпасныхДействий", Истина);
	
	Возврат ОписаниеПользователя;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПользователейНаСервере(ДанныеДляЗагрузки)
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	ОбъектОбработка.ВыполнитьЗагрузкуПользователей(ДанныеДляЗагрузки);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУникальностьЗначений()
	
	ПолныеИмена			= Новый Массив;
	КраткиеИмена			= Новый Массив;
	ПользователиОС		= Новый Массив;
	АдресЭлектроннойПочты	= Новый Массив;
	
	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		ПроверитьЗначениеСтроки(ПолныеИмена,			НомерСтроки, 1);
		ПроверитьЗначениеСтроки(КраткиеИмена,			НомерСтроки, 2);
		ПроверитьЗначениеСтроки(ПользователиОС,			НомерСтроки, 3);
		ПроверитьЗначениеСтроки(АдресЭлектроннойПочты,	НомерСтроки, 4);
	КонецЦикла;
	
КонецПроцедуры // ПроверитьУникальностьЗначений()

&НаКлиенте
Процедура ПроверитьЗначениеСтроки(МассивЗначений, НомерСтроки, НомерКолонки)

	ОбластьЗначений = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
	
	Если МассивЗначений.Найти(ОбластьЗначений.Текст) = Неопределено Тогда
		МассивЗначений.Добавить(ОбластьЗначений.Текст);
		ОбластьЗначений.ЦветФона	= Новый Цвет;
		ОбластьЗначений.ЦветТекста	= Новый Цвет;
	Иначе
		ОбластьЗначений.ЦветФона	= WebЦвета.Льняной;
		ОбластьЗначений.ЦветТекста	= WebЦвета.ТемноКрасный;
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("В строке %1 в колонке %2 указано неуникальное значение!", 
															НомерСтроки, НомерКолонки));
	КонецЕсли;

КонецПроцедуры // ПроверитьЗначениеСтроки()

&НаКлиенте
Процедура ЗаполнитьПараметрыВСтроке(НомерСтроки, МассивУникальныхИмен);
	
	СтруктураЗначений = Новый Структура;
	СтруктураЗначений.Вставить("ПолноеИмя",							"");
	СтруктураЗначений.Вставить("Имя",									"");
	СтруктураЗначений.Вставить("АдресЭлектроннойПочты",				"");
	СтруктураЗначений.Вставить("ПользовательОС",						"");
	СтруктураЗначений.Вставить("Пароль",								"");
	СтруктураЗначений.Вставить("ПраваДоступа",							"");
	СтруктураЗначений.Вставить("Комментарий",							"");
	СтруктураЗначений.Вставить("ПоказыватьВСпискеВыбора",			Ложь);
	СтруктураЗначений.Вставить("ПотребоватьСменуПароляПриВходе",	Ложь);
	СтруктураЗначений.Вставить("ЗапрещеноИзменятьПароль",			Ложь);
	СтруктураЗначений.Вставить("ЗапрещеноВосстанавливатьПароль",	Ложь);
	СтруктураЗначений.Вставить("АутентификацияСтандартная",			Ложь);
	СтруктураЗначений.Вставить("АутентификацияОС",					Ложь);
	СтруктураЗначений.Вставить("АутентификацияOpenID",				Ложь);
	СтруктураЗначений.Вставить("АутентификацияOpenIDConnect",		Ложь);
	СтруктураЗначений.Вставить("АутентификацияТокеномДоступа",		Ложь);
	
	ОбластьПолноеИмя = ТабличныйДокумент.Область(НомерСтроки, Колонки.ПолноеИмя, 
											НомерСтроки, Колонки.ПолноеИмя);

	СтруктураЗначений.ПолноеИмя = ОбластьПолноеИмя.Текст;
	
	Если ПустаяСтрока(СтруктураЗначений.ПолноеИмя) Тогда
		ЗаполнитьТекстЯчеек(НомерСтроки, СтруктураЗначений); 
		Возврат;
	КонецЕсли;	

	ПолноеИмяБезСимволов = УдалитьСимволы(ОбластьПолноеИмя.Текст);
	
	СтруктураИмени = ЧастиИмени(ПолноеИмяБезСимволов);
	
	ПривестиКУникальнымИмена(СтруктураИмени, МассивУникальныхИмен);
	СтруктураЗначений.Имя = СтруктураИмени.ИмяПользователяИБ;
	
	СтруктураЗначений.АдресЭлектроннойПочты	= ЗаполнитьСтрокуПоШаблону(ШаблонПочты, СтруктураИмени);
	СтруктураЗначений.ПользовательОС				= ЗаполнитьСтрокуПоШаблону(ШаблонДомена, СтруктураИмени);
	
	Если ЗначениеЗаполнено(ПарольПоУмолчанию) Тогда
		СтруктураЗначений.Пароль = ПарольПоУмолчанию;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПравДоступа) Тогда
		СтруктураЗначений.ПраваДоступа = ДанныеПравДоступа;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		СтруктураЗначений.Комментарий = Комментарий;
	КонецЕсли;
	
	СписокСвойств = "ПоказыватьВСпискеВыбора, 
						|ПотребоватьСменуПароляПриВходе, 
						|ЗапрещеноИзменятьПароль, 
						|ЗапрещеноВосстанавливатьПароль, 
						|АутентификацияСтандартная, 
						|АутентификацияОС, 
						|АутентификацияOpenID, 
						|АутентификацияOpenIDConnect, 
						|АутентификацияТокеномДоступа";
	
	ЗаполнитьЗначенияСвойств(СтруктураЗначений, ЭтотОбъект, СписокСвойств);
	
	ЗаполнитьТекстЯчеек(НомерСтроки, СтруктураЗначений);
	
КонецПроцедуры 

&НаКлиенте
Функция ЗаполнитьСтрокуПоШаблону(Знач СтрокаШаблона, СтруктураИмени)
	
	СоответствиеПараметров = Новый Соответствие;
	СоответствиеПараметров.Вставить("f", "ИнициалыФамилия");
	СоответствиеПараметров.Вставить("i", "ИнициалыИмя");
	СоответствиеПараметров.Вставить("o", "ИнициалыОтчество");
	СоответствиеПараметров.Вставить("F", "Фамилия");
	СоответствиеПараметров.Вставить("I", "Имя");
	СоответствиеПараметров.Вставить("O", "Отчество");
	
	Для Каждого ЭлементСоответствия Из СоответствиеПараметров Цикл
		
		ЗначениеИмени = СтруктураИмени[ЭлементСоответствия.Значение];
		ЗначениеЛатиницей = НРег(СтроковыеФункцииКлиент.СтрокаЛатиницей(ЗначениеИмени));
		СтрокаШаблона = СтрЗаменить(СтрокаШаблона, "%" + ЭлементСоответствия.Ключ, ЗначениеЛатиницей);
		
	КонецЦикла;
	
	Возврат СтрокаШаблона;
		
КонецФункции

&НаКлиенте
Процедура ПривестиКУникальнымИмена(СтруктураИмени, МассивУникальныхИмен, СчетчикДублей = 0)
	
	ДопСимволИмени	= Макс(1, Окр(СчетчикДублей / 2));
	ДопСимволОтчества	= Макс(1, СчетчикДублей - ДопСимволИмени);
	
	ДопСимволИмени	= Мин(ДопСимволИмени,		СтрДлина(СтруктураИмени.Имя));
	ДопСимволОтчества	= Мин(ДопСимволОтчества,	СтрДлина(СтруктураИмени.Отчество));
	
	ИнициалыФамилия	= Лев(СтруктураИмени.Фамилия, 1);
	ИнициалыИмя		= Лев(СтруктураИмени.Имя, ДопСимволИмени);
	ИнициалыОтчество	= Лев(СтруктураИмени.Отчество, ДопСимволОтчества);
	
	КраткоеФИО			= ИнициалыИмя + ИнициалыОтчество + СтруктураИмени.Фамилия;
	ИмяПользователяИБ	= СтруктураИмени.Фамилия + ИнициалыИмя + ИнициалыОтчество;
	
	ДубльЗначения = МассивУникальныхИмен.Найти(КраткоеФИО);
	Если ДубльЗначения = Неопределено Тогда
		МассивУникальныхИмен.Добавить(КраткоеФИО);
		СтруктураИмени.ИмяПользователяИБ	= ИмяПользователяИБ;
		СтруктураИмени.КраткоеФИО			= КраткоеФИО;
		СтруктураИмени.ИнициалыИмя		= ИнициалыИмя;
		СтруктураИмени.ИнициалыФамилия	= ИнициалыФамилия;
		СтруктураИмени.ИнициалыОтчество	= ИнициалыОтчество;
	Иначе
		СчетчикДублей = СчетчикДублей + 1;
		ПривестиКУникальнымИмена(СтруктураИмени, МассивУникальныхИмен, СчетчикДублей);
	КонецЕсли;

КонецПроцедуры // ПолучитьУникальноеИмя()

&НаСервереБезКонтекста
Функция ПолучитьНовыйПароль(СвойстваПароля, НомерРелизаПлатформы)
	
	ДлинаПароля		= 7;
	СложныйПароль	= Ложь;
	
	Для Каждого ПарольнаяПолитика Из ПолитикиПаролейПользователей.ПолучитьПолитики() Цикл
		ДлинаПароля		= Макс(ПарольнаяПолитика.МинимальнаяДлинаПаролей, ДлинаПароля);
		СложныйПароль	= Макс(ПарольнаяПолитика.ПроверкаСложностиПаролей, СложныйПароль);
	КонецЦикла;
	
	СвойстваПароля.НаименьшаяДлина	= ДлинаПароля;
	СвойстваПароля.Сложный		= СложныйПароль;
	
	Если НомерРелизаПлатформы < 22 Тогда
		НовыйПароль = Пользователи.СоздатьПароль(СвойстваПароля);
		НовыйПароль = СтрЗаменить(НовыйПароль, " ", "_");
	Иначе
		ГенераторСлучайныхПаролей = Новый ГенераторСлучайныхПаролей; 
		НовыйПароль = ГенераторСлучайныхПаролей.СлучайныйПароль(ДлинаПароля);
	КонецЕсли;
	
	Возврат НовыйПароль;
	
КонецФункции // ПолучитьНовыйПароль()

&НаКлиенте
Функция УдалитьСимволы(Знач ИсходнаяСтрока)
	
	СписокЗапрещенныхСимволов = "~`!@#$%^&*()_+№;%:?[]{}\|'/,""";
	ИсходнаяСтрока = СтрСоединить(СтрРазделить(ИсходнаяСтрока, СписокЗапрещенныхСимволов)," ");
	Возврат СтрЗаменить(ИсходнаяСтрока, "  ", " ");

КонецФункции // УдалитьСимволы()

&НаКлиенте
Процедура ЗаполнитьТекстЯчеек(НомерСтроки, СтруктураЗначений)
	
	СписокПолей = "Пароль,ПраваДоступа,Комментарий"; //После запятой не должно быть пробелов
	
	ПроверяемыеПоля = СтрРазделить(СписокПолей, ",");
	
	Для Каждого КолонкаТаблицы Из Колонки Цикл
		ОбластьЗначения = ТабличныйДокумент.Область(НомерСтроки, КолонкаТаблицы.Значение, 
												НомерСтроки, КолонкаТаблицы.Значение);
		Значение = СтруктураЗначений[КолонкаТаблицы.Ключ];
		
		Если ПроверяемыеПоля.Найти(КолонкаТаблицы.Ключ) <> Неопределено 
			И Не ПустаяСтрока(ОбластьЗначения.Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьЗначения.Текст = Формат(Значение, "БЛ=; БИ=+");
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьТекстЯчеек()

// Раскладывает полное имя физического лица на составные части - фамилию, имя и отчество.
// Если в конце полного имени встречаются "оглы", "улы", "уулу", "кызы" или "гызы",
// то они также считаются частью отчества.
//
// Параметры:
//  ФамилияИмяОтчество - Строка - полное имя в виде "Фамилия Имя Отчество".
//
// Возвращаемое значение:
//  Структура:
//   * Фамилия  - Строка - фамилия;
//   * Имя      - Строка - имя;
//   * Отчество - Строка - отчество.
//
// Пример:
//   1. ФизическиеЛицаКлиентСервер.ЧастиИмени("Иванов Иван Иванович") 
//   вернет структуру со значениями свойств: "Иванов", "Иван", "Иванович".
//   2. ФизическиеЛицаКлиентСервер.ЧастиИмени("Смит Джон") 
//   вернет структуру со значениями свойств: "Смит", "Джон", "".
//   3. ФизическиеЛицаКлиентСервер.ЧастиИмени("Алиев Ахмед Октай оглы") 
//   вернет структуру со значениями свойств: "Алиев", "Ахмед", "Октай оглы".
// 
&НаКлиенте
Функция ЧастиИмени(ФамилияИмяОтчество) 
	
	Результат = Новый Структура;
	Результат.Вставить("Фамилия");
	Результат.Вставить("Имя");
	Результат.Вставить("Отчество");
	Результат.Вставить("КраткоеФИО");
	Результат.Вставить("ИмяПользователяИБ");
	Результат.Вставить("ИнициалыФамилия");
	Результат.Вставить("ИнициалыИмя");
	Результат.Вставить("ИнициалыОтчество");
	
	ЧастиИмени = СтрРазделить(ФамилияИмяОтчество, " ", Ложь); 
	
	КоличествоДополнений = 0;
	
	Если ЧастиИмени.Количество() >= 1 Тогда
		Результат.Фамилия = ЧастиИмени[0];
	КонецЕсли;
	
	Если ЧастиИмени.Количество() >= 2 Тогда
		ПервыеЧастиИмени = Новый Массив;
		ПервыеЧастиИмени.Добавить(НСтр("ru = 'Абдул'"));
		ПервыеЧастиИмени.Добавить(НСтр("ru = 'Абу'"));
		
		Результат.Имя = ЧастиИмени[1];
		Если ПервыеЧастиИмени.Найти(ТРег(ЧастиИмени[1])) <> Неопределено 
			И ЧастиИмени.Количество() >= 3 Тогда
			КоличествоДополнений = КоличествоДополнений + 1;
			Результат.Имя = Результат.Имя + " " + ЧастиИмени[2];
		КонецЕсли;
	КонецЕсли;
	
	Если ЧастиИмени.Количество() >= 3 + КоличествоДополнений Тогда
		Результат.Отчество = ЧастиИмени[2 + КоличествоДополнений];
	КонецЕсли;
	
	Если ЧастиИмени.Количество() > 3 + КоличествоДополнений Тогда
		ДополнительныеЧастиОтчества = Новый Массив;
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'оглы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'улы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'уулу'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'кызы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'гызы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'угли'"));
		
		Если ДополнительныеЧастиОтчества.Найти(НРег(ЧастиИмени[3 + КоличествоДополнений])) <> Неопределено Тогда
			Результат.Отчество = Результат.Отчество + " " + ЧастиИмени[3 + КоличествоДополнений];
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьДанныеВJSON(ПутьКФайлу)
	
	ДанныеПользователей	= ПолучитьДанныеТабличногоДокумента();
	Настройки				= ПолучитьНастройки();
	
	Запись = Новый ЗаписьJSON;
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись.ОткрытьФайл(ПутьКФайлу, , , ПараметрыЗаписиJSON);
	
	ДанныеЭкспорта = Новый Структура;
	ДанныеЭкспорта.Вставить("Настройки",			Настройки);
	ДанныеЭкспорта.Вставить("ДанныеПользователей",	ДанныеПользователей);

	ЗаписатьJSON(Запись, ДанныеЭкспорта, Новый НастройкиСериализацииJSON);
	Запись.Закрыть();
	
КонецПроцедуры // ЗаписатьДанныеВJSON()

&НаКлиенте
Функция ПрочитатьДанныеИзJSON(ПутьКФайлу)

	Чтение = Новый ЧтениеJSON;
	Чтение.ОткрытьФайл(ПутьКФайлу);

	ДанныеИмпорта = ПрочитатьJSON(Чтение);
	
	Чтение.Закрыть();
	
	Возврат ДанныеИмпорта;

КонецФункции // ПрочитатьДанныеИзJSON()

&НаКлиенте
Функция ПолучитьНастройки()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("Автозаполнение");
	СтруктураНастроек.Вставить("АутентификацияСтандартная");
	СтруктураНастроек.Вставить("ПоказыватьВСпискеВыбора");
	СтруктураНастроек.Вставить("ПотребоватьСменуПароляПриВходе");
	СтруктураНастроек.Вставить("ЗапрещеноИзменятьПароль");
	СтруктураНастроек.Вставить("ЗапрещеноВосстанавливатьПароль");
	СтруктураНастроек.Вставить("АутентификацияОС");
	СтруктураНастроек.Вставить("АутентификацияOpenID");
	СтруктураНастроек.Вставить("АутентификацияOpenIDConnect");
	СтруктураНастроек.Вставить("АутентификацияТокеномДоступа");
	
	СтруктураНастроек.Вставить("ШаблонПочты");
	СтруктураНастроек.Вставить("ШаблонДомена");
	СтруктураНастроек.Вставить("ПарольХранимоеЗначение");
	СтруктураНастроек.Вставить("СпособУстановкиПароля");
	СтруктураНастроек.Вставить("ПарольПоУмолчанию");
	СтруктураНастроек.Вставить("ВариантУстановкиДоступа");
	СтруктураНастроек.Вставить("ДанныеПравДоступа");
	СтруктураНастроек.Вставить("Комментарий");
	СтруктураНастроек.Вставить("КоличествоПользователей");
	СтруктураНастроек.Вставить("Постфикс");
	
	ЗаполнитьЗначенияСвойств(СтруктураНастроек, ЭтотОбъект);
	
	Возврат СтруктураНастроек;

КонецФункции // ПолучитьНастройки()

&НаКлиенте
Процедура УстановитьНастройки(Настройки)

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);

КонецПроцедуры // УстановитьНастройки()

&НаКлиенте
Процедура УстановитьДанныеТабличногоДокумента(ДанныеПользователей)
	
	СписокПолейЗаполнеия = ПоляЗаполнения();
	
	ОчиститьПолеДокумента();
	НомерСтроки = 2;
	Для Каждого ОписаниеПользователя Из ДанныеПользователей Цикл  
		
		ЗаполнитьДанныеСтрокиЗначениями(ОписаниеПользователя, НомерСтроки, СписокПолейЗаполнеия);
		
		Если ВариантУстановкиДоступа = 0 Тогда
			ПравДоступа = ОписаниеПользователя.ПользовательОснование;
		Иначе
			ПравДоступа = ОписаниеПользователя.ГруппыДоступа;
		КонецЕсли;
		
		ТабличныйДокумент.Область(НомерСтроки, Колонки.ПраваДоступа, 
								НомерСтроки, Колонки.ПраваДоступа).Текст = ПравДоступа;
								
		Если ПарольХранимоеЗначение = 0 Тогда
			Пароль = ОписаниеПользователя.Пароль;
		Иначе
			Пароль = ОписаниеПользователя.СохраняемоеЗначениеПароля;
		КонецЕсли;
		
		ТабличныйДокумент.Область(НомерСтроки, Колонки.Пароль, 
								НомерСтроки, Колонки.Пароль).Текст = Пароль;
								
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
							
КонецПроцедуры // УстановитьДанныеТабличногоДокумента()

&НаКлиенте
Процедура ЗаполнитьДанныеСтрокиЗначениями(ОписаниеПользователя, НомерСтроки, ПоляЗаполнения)

	Для Каждого ИмяПоля Из ПоляЗаполнения Цикл
		
		ОбластьЗначения = ТабличныйДокумент.Область(НомерСтроки, Колонки[ИмяПоля], 
												НомерСтроки, Колонки[ИмяПоля]);
		
		Если ТипЗнч(ОписаниеПользователя[ИмяПоля]) = Тип("Булево") Тогда
			ОбластьЗначения.Текст = Формат(ОписаниеПользователя[ИмяПоля], "БЛ=; БИ=+");
		Иначе
			ОбластьЗначения.Текст = ОписаниеПользователя[ИмяПоля];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьЗначенияПоДаннымСтроки()

&НаКлиенте
Процедура ОчиститьПолеДокумента()
	
	ОчиститьПолеДокументаНаСервере();

КонецПроцедуры // ОчиститьПолеДокумента()

&НаСервере
Процедура ОчиститьПолеДокументаНаСервере()
	
	ОбластьШапки = ТабличныйДокумент.ПолучитьОбласть(1, 1, 1, Колонки.Количество());
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ОбластьШапки);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоляЗаполнения()

	ПоляЗаполнения = Новый Массив;
	ПоляЗаполнения.Добавить("Имя");
	ПоляЗаполнения.Добавить("ПолноеИмя");
	ПоляЗаполнения.Добавить("АдресЭлектроннойПочты");
	ПоляЗаполнения.Добавить("ПользовательОС");
	ПоляЗаполнения.Добавить("Комментарий");
	ПоляЗаполнения.Добавить("ПоказыватьВСпискеВыбора");
	ПоляЗаполнения.Добавить("ПотребоватьСменуПароляПриВходе");
	ПоляЗаполнения.Добавить("ЗапрещеноИзменятьПароль");
	ПоляЗаполнения.Добавить("ЗапрещеноВосстанавливатьПароль");
	ПоляЗаполнения.Добавить("АутентификацияСтандартная");
	ПоляЗаполнения.Добавить("АутентификацияОС");
	ПоляЗаполнения.Добавить("АутентификацияOpenID");
	ПоляЗаполнения.Добавить("АутентификацияOpenIDConnect");
	ПоляЗаполнения.Добавить("АутентификацияТокеномДоступа");
	
	Возврат ПоляЗаполнения;

КонецФункции // ПоляЗаполнения()

&НаКлиенте
Процедура ЗаполнитьДанныеКолонки(НомерКолонки, Значение, Перезаполнять = Истина)

	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		
		ОбластьЯчейкиЗначения = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		Если Не ЗначениеЗаполнено(ОбластьЯчейкиЗначения.Текст) Или Перезаполнять Тогда
			ОбластьЯчейкиЗначения.Текст = Значение;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьДанныеКолонки()

&НаКлиенте
Процедура ОповещенияВыбораПравДоступа(Источник, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Источник) = Тип("Массив") Тогда
		ДанныеПравДоступа = СтрСоединить(Источник, Символы.ПС);
	Иначе
		ДанныеПравДоступа = Строка(Источник);
	КонецЕсли;

КонецПроцедуры // ПриОткрытииПослеПодтвержденияОбновленияВидовДоступа()

#КонецОбласти
